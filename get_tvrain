#!/usr/bin/perl

###
###	⛈	optimistiCli: TVRain.ru command line client intended to provide legitimate paying users access to the video archive.
###

use utf8;
use strict;

our $usage = 'Usage:
  get_tvrain [-h|--long-help] [-v] [-F] [-c <merge|save|pls>] [-q <hd|sd|lo>] 
             [-d <output dir>] [-o <output file name>] [-t <temp dir>] 
             [-i] [-y] [-C <config file>] <Video ID | Page with video URL> 

TVRain.ru show archive command line client. Legitimate paying users only! 

To download a show in current directory run: 
  get_tvrain http://tvrain.ru/teleshow/reportazh/the_rain_is_falling-123456/

To get detailed options description run: 
  get_tvrain --long-help
';

our $long_help = $usage . '
Options:
  -h Show usage and exit
  -v Verbose output, prints a lot of debugging stuff to STDERR
  -F Force first run, resets all options, see below
  -c Command, instructs get_tvrain what to do:
       merge - downloads all parts of the video and merges them into one 
               matroska file, requires temp directory for downloaded parts, 
               this is the default command
       save  - saves each part of the video as is
       pls   - does not download any video, saves a playlist with video URLs
  -q Video quality selector, available options are:
       hd - high definition aka 720p (default)
       sd - standard definition aka 480p
       lo - low bitrate aka 360p
  -d Write output file(s) to the specified directory, defaults to current 
     directory
  -o Set output file(s) name, if omitted the article / feature name is used 
     to compose the file name(s); overrides -d and -i
  -t Use specified directory to temporarily store downloaded video parts for 
     the "merge" command, if omitted looks up temp_dir in the configuration 
     file, which defaults to the current directory
  -i Use video ID for the file(s) name
  -y Override output file(s) in case of naming conflict, if not specified 
     file name(s) will be changed to avoid overwriting
  -C Use specified configuration file instead of the default one

Examples:
  Download a show by URL, if video consists of several parts merge them 
  together. Use /tmp to store temporary files.
  get_tvrain -t /tmp http://tvrain.ru/teleshow/reportazh/falling_rain-123456/

  Download a show known by its ID to the Desktop. Select low bitrate to 
  preserve the bandwidth. If there are several video files save them 
  separately. Name files using show ID.
  get_tvrain -d ~/Desktop -q lo -c save -i 123456

  Create a custom-named playlist of the standard definition videos. If a 
  playlist by the same name already exists overwrite it.
  get_tvrain -q sd -o /mnt/autoplay/tvrain.pls -c pls -y 123456

Configuration file:
  The configuration file is .get_tvrain.conf in your home directory, This file 
  is created automatically during the first run. Use -C option to run with 
  alternative configuration file. File format is name=value, no whitespaces 
  around the "=", no comments after the value are allowed. Encoding is UTF-8.
  Noteworthy options:
    device_uid
      Client-side hardware unique identifier / serial number. In fact this 
      value is randomly generated during the first run.
    auth
      Authentication key. This value is obtained from the server when the 
      client is linked to TVRain.ru user account as a part of the first run 
      routine.
    temp_dir
      The directory to temporarily store downloaded video parts for the 
      "merge" command. Please make sure the directory is on a reasonably fast 
      storage device (ie the use of thumb drives and flash cards is 
      discouraged) and that it has enough free space (4Gb would do). If not 
      set the current directory or the value passed with the -t option is 
      used.

First run:
  When run for the first time the "first run routine" is invoked to configure 
  the application. It does as follows:
    1. configuration file is created
    2. unique identifier is randomly generated
    3. client is linked to the TVRain.ru user account
    4. temporary directory is set
  To clear all the settings the first run routine can be re-run manually 
  by specifying the -F option or just by deleting the configuration file.

Linking:
  In order to gain access to the TVRain.ru show archive the client software 
  should be "linked" to an active paid user account. Linking happens during 
  the first run and requires internet access and user interaction.

';

our $linking_hint = 'Q What to do next?
A 1. Open http://tvrain.ru/profile/ in a brwoser
  2. Log into an account
  3. Browse to "Profile" → "Devices"
  4. Enter the linking code (see above) and perss "Link"
  5. Wait until get_tvrain resumes\n

';

our $relink_hint = '

You might want to re-link the client by specifying the -F option.
For more details please run:
get_tvran --long-help
';


use open IO  => ":encoding(utf8)";
binmode STDOUT, ":utf8";
binmode STDERR, ":utf8";

use Env qw(HOME);
use Config::Tiny;
use HTTP::Headers;
use HTTP::Request;
use CGI::Utils;
use LWP::UserAgent;
use JSON;
use MIME::Base64 qw(encode_base64);
use Getopt::Long qw(:config no_ignore_case require_order bundling);
use IO::Scalar;


### Constants

our $CONFIG_FILE_NAME = '.get_tvrain.conf';
our $DEVICE_NAME = 'NothingBut Client'; #'optimistiCli';
our $URL_BASE = 'https://api.tvrain.ru/api_v2';
our $API_CONSUMER_KEY = 'a908545f-80af-4f99-8dac-fb012cec';
our $API_VERSION = '2.13';
our $LINK_POLL_TIMEOUT_SEC=15;
our $COMMAND_MERGE = 'merge';
our $COMMAND_SAVE = 'save';
our $COMMAND_PLAYLIST = 'pls';
our $QUALITY_HD = 'hd';
our $QUALITY_SD = 'sd';
our $QUALITY_LO = 'lo';


### Globals

our $options;	# Command line options
our $config;	# Config file values
our $session;	# Session-wide values

# TODO: Integrate following into session
our $cgi_utils;
our $user_agent;
our $video_id;

### Init

$cgi_utils = CGI::Utils->new;
$user_agent = LWP::UserAgent->new;

# Set options defaults
$options = {
	'help'				=> 0,
	'long-help'		=> 0,
	'verbose'			=> 0,
	'first-run'		=> 0,
	'command'			=> $COMMAND_MERGE,
	'quality'			=> $QUALITY_HD,
	'output-dir'	=> undef,
	'output-file'	=> undef,
	'temp-dir'		=> undef,
	'id'					=> 0,
	'yes-all'			=> 0,
	'config'			=> undef,
};

### Functions

sub dumpit;

sub dumpit {
	my $workload = shift;
	my $name = shift;
	my $depth = shift;

	$depth ||= 0;

	my $buffer = '';

	my $type = ref($workload);

	if ($type eq 'ARRAY') {
		$buffer .= '  ' x $depth;
		$buffer .= $name . ' → ' if defined $name;
		$buffer .= "[\n";

		for (my $i = 0 ; $i < scalar @$workload; $i++) {
			$buffer .= dumpit $workload->[$i], $i, $depth + 1;
		}

		$buffer .= '  ' x $depth;
		$buffer .= "]\n";
	} elsif ($type eq 'HASH') {
		$buffer .= '  ' x $depth;
		$buffer .= $name . ' → ' if defined $name;
		$buffer .= "{\n";

		foreach my $key (sort keys %$workload) {
			$buffer .= dumpit $workload->{$key}, $key, $depth + 1;
		}

		$buffer .= '  ' x $depth;
		$buffer .= "}\n";
	} else {
		$buffer .= '  ' x $depth;
		$buffer .= $name . ' → ' if defined $name;
		$buffer .= "$workload\n";
	}

	return $buffer;
}

# TODO: Implement 2-level verbosity with no operational output on base level.
sub debug {
	my $title = shift;
	my $text = shift;
	
	$title = uc $title;
	print STDERR "============ $title START ============\n$text\n============= $title END =============\n\n";
}

sub brag_and_exit {
	my $err_message = shift;
	my $exit_code = shift;

	if ($err_message) {
		chomp $err_message;
		$err_message =~ s/^/  /gms;
		$err_message = "Error:\n$err_message\n\n";
		$exit_code = 1 unless $exit_code;
	}

	print $err_message, $usage, "\n";
	exit $exit_code;
}

# Creates an empty file and immediately deletes it
sub get_file_creation_error {
	my $file_path = shift;

	return "File exists '$file_path" if -e $file_path;

	open FH, "> $file_path" or return "Can not write to '$file_path', $!";
	close FH;

	unlink $file_path or die "Failed to delete touched file '$file_path'";

	return undef;
}

sub check_if_dir_is_wrieable {
	my $dir_path = shift;
	my $dir_name = shift;

	return unless defined $dir_path;

	brag_and_exit "$dir_name '$dir_path' does not exist" unless -e $dir_path;

	brag_and_exit "$dir_name '$dir_path' is not a directory" unless -d $dir_path;

	brag_and_exit "$dir_name '$dir_path' can not be written to" unless -w $dir_path;

	$dir_path;
}

sub check_option_command {
	# Command should be set, otherwise there is an error
	$_ = $options->{'command'};

	$options->{'command'} = 
		/^merge|mkv|join|matroska$/i ? $COMMAND_MERGE :
		/^save|download|dl|asis|mp4|dump$/i ? $COMMAND_SAVE :
		/^pls|playlist|pls|play.list|m3u|m3u8$/i ? $COMMAND_PLAYLIST :
		brag_and_exit "Unknown command $_";

	debug 'command', $options->{'command'} if $options->{'verbose'};
}

sub check_option_quality {
	# Quality should be set, otherwise there is an error
	$_ = $options->{'quality'};

	$options->{'quality'} = 
		/^hd|hq|high|hi|720|720p$/i ? $QUALITY_HD :
		/^sd|sq|stand.*|std|480|480p$/i ? $QUALITY_SD :
		/^lo|ld|lq|lo|low|360|360p$/i ? $QUALITY_LO :
		brag_and_exit "Unknown quality $_";

	debug 'quality', $options->{'quality'} if $options->{'verbose'};
}

sub check_option_output_dir {
	return unless defined $options->{'output-dir'};

	$options->{'output-dir'} = check_if_dir_is_wrieable $options->{'output-dir'}, 'Output directory';

	debug 'output dir', $options->{'output-dir'} if $options->{'verbose'};
}

sub check_option_output_file {
	return unless defined $options->{'output-file'};

	if (-e $options->{'output-file'}) {
		# File exist: check if it is a proper file and if it can be overwritten

		brag_and_exit "Can not overwrite non-file path '$options->{'output-file'}'" unless -f $options->{'output-file'};

		# TODO: Simlinks get through the -f above. Decide if they need some special tratment.

		brag_and_exit "Can not overwrite file '$options->{'output-file'}'" unless -w $options->{'output-file'};
	} else {
		# File does not exist: check if it can be created and written to

		my $file_creation_error = get_file_creation_error $options->{'output-file'};
		brag_and_exit $file_creation_error if defined $file_creation_error;
	}

	debug 'output file', $options->{'output-file'} if $options->{'verbose'};
}

sub check_option_temp_dir {
	return unless defined $options->{'temp-dir'};

	$options->{'temp-dir'} = check_if_dir_is_wrieable $options->{'temp-dir'}, 'Temporary directory';

	debug 'temp dir', $options->{'temp-dir'} if $options->{'verbose'};
}

sub check_option_config {
	return unless defined $options->{'config'};

	if (-e $options->{'config'}) {
		brag_and_exit "Config file '$options->{'config'}' can not be written to" unless -w $options->{'config'};

		# Avoid accidental damage: accept config file only if the file is a proper config file or if it is an empty file
		brag_and_exit "Cowardly refusing to use non-config file '$options->{'config'}'" 
			unless defined Config::Tiny->read($options->{'config'}, 'utf8') 
				|| (stat($options->{'config'}))[7] == 0;
	} else {
		my $file_creation_error = get_file_creation_error $options->{'config'};
		brag_and_exit $file_creation_error if defined $file_creation_error;
	}

	debug 'config', $options->{'config'} if $options->{'verbose'};
}

sub cook_rand_xxxx {
	sprintf "%04X", int rand 0x10000;
}

sub cook_device_uid {
	&cook_rand_xxxx . '-' . 
	&cook_rand_xxxx . '-' . 
	&cook_rand_xxxx . '-' . 
	&cook_rand_xxxx . '-' . 
	&cook_rand_xxxx . '-' . 
	&cook_rand_xxxx . '-' . 
	&cook_rand_xxxx . '-' . 
	&cook_rand_xxxx;
}

sub cook_quickauth_request {
	{
		'name'					=> 'quickauth',
		'method'				=> 'POST',
		'url'						=> "$URL_BASE/user/quickauth/",
		'post_params'		=> {
			'device_uid'		=> $config->{_}->{'device_uid'},
		},
		'extra_headers'	=> {
		},
		'error_overrides'	=> {
		},
	}
}

sub cook_startlinking_request {
	{
		'name'					=> 'startlinking',
		'method'				=> 'POST',
		'url'						=> "$URL_BASE/user/startlinking/",
		'post_params'		=> {
		},
		'extra_headers'	=> {
		},
		'error_overrides'	=> {
		},
	}
}

sub cook_link_request {
	my $oid = shift;

	{
		'name'						=> 'link',
		'method'					=> 'POST',
		'url'							=> "$URL_BASE/user/link/",
		'post_params'			=> {
			'device_uid'			=> $config->{_}->{'device_uid'},
			'oid'							=> $oid,
		},
		'extra_headers'		=> {
		},
		'error_overrides'	=> {
			'401'							=> {
				'error'						=> '401 Unauthorized',
			}
		},
	}
}

sub cook_me_request {
	{
		'name'						=> 'me',
		'method'					=> 'GET',
		'url'							=> "$URL_BASE/user/me/",
		'post_params'			=> {
		},
		'extra_headers'		=> {
		},
		'error_overrides'	=> {
			'403'							=> {
				'error'						=> '403 Forbidden',
			}
		},
	}
}

sub cook_videos_request {
	my $video_id = shift;

	{
		'name'						=> 'videos',
		'method'					=> 'GET',
		'url'							=> "$URL_BASE/articles/$video_id/videos/",
		'post_params'			=> {
		},
		'extra_headers'		=> {
		},
		'error_overrides'	=> {
			'404'							=> [{
				'error'						=> '404 Not Found',
			}]
		},
	}
}

sub write_config {
	$config->write($session->{'config_file_path'}, 'utf8') or die "Error writing config " . $config->errstr();
}

sub cook_auth {
	my $user_id = shift;
	my $device_token = shift;

	'Basic ' . encode_base64 "$user_id:$device_token", '';
}

sub call_api {
	my $api_request = shift;

	# TODO: Try implementing via $user_agent->default_headers
	my $headers = HTTP::Headers->new(
		'X-User-Agent'									=> "$DEVICE_NAME; API_CONSUMER_KEY=$API_CONSUMER_KEY",
		'Accept'												=> "application/tvrain.api.$API_VERSION+json",
		'Content-Type'									=> 'application/x-www-form-urlencoded',
	);

	my $encoded_params = $cgi_utils->url_encode_vars($api_request->{'post_params'}, '&') if keys %{$api_request->{'post_params'}};
	$api_request->{'extra_headers'}->{'Content-Length'} = length $encoded_params if $encoded_params;

	foreach my $key (keys %{$api_request->{'extra_headers'}}) {
		$headers->push_header($key => $api_request->{'extra_headers'}->{$key});
	}

	$headers->push_header('Authorization' => $config->{_}->{'auth'}) if defined $config->{_}->{'auth'};

	my $request = HTTP::Request->new(
		$api_request->{'method'},
		$api_request->{'url'},
		$headers,
		$encoded_params
	);

	debug 'request', $request->as_string if $options->{'verbose'};

	my $response = $user_agent->request($request);

	if ($response->is_success) {
		debug 'response', $response->content if $options->{'verbose'};

		return decode_json $response->content;
	} else {
		if (defined $api_request->{'error_overrides'}->{$response->code}) {
			debug 'response', 'Executing error override for HTTP return code ' . $response->code if $options->{'verbose'};

			return $api_request->{'error_overrides'}->{$response->code};
		} else {
			die "Error processing “$api_request->{'name'}” API call: " . $response->status_line;
		}
	}
}

sub read_command_line_options {
	my $error_message;
	my $error = tie *STDERR, 'IO::Scalar', \$error_message;

	my $all_went_well = GetOptions ($options, 
		'help|h',
		'long-help',
		'verbose|v',
		'first-run|F',
		'command|c=s',
		'quality|q=s',
		'output-dir|d=s',
		'output-file|o=s',
		'temp-dir|t=s',
		'id|i',
		'yes-all|y',
		'config|C=s',
	);

	undef $error;
	untie *STDERR;

	brag_and_exit $error_message unless $all_went_well;
}

sub deal_with_help_requests {
	print $long_help and exit if $options->{'long-help'};

	&brag_and_exit if $options->{'help'};
}

sub interpret_command_line_options_to_merge {
	&check_option_quality;
	&check_option_output_dir;
	&check_option_output_file;
	&check_option_temp_dir;
}

sub interpret_command_line_options_to_save {
	&check_option_quality;
	&check_option_output_dir;
	&check_option_output_file;
}

sub interpret_command_line_options_to_playlist {
	&check_option_quality;
	&check_option_output_dir;
	&check_option_output_file;
}

sub interpret_command_line_options {
	&check_option_command;

	if ($options->{'command'} eq $COMMAND_MERGE) {
		&interpret_command_line_options_to_merge;
	} elsif ($options->{'command'} eq $COMMAND_SAVE) {
		&interpret_command_line_options_to_save;
	} elsif ($options->{'command'} eq $COMMAND_PLAYLIST) {
		&interpret_command_line_options_to_playlist;
	} else {
		die "Impossible command";
	}

	&check_option_config;
}

sub get_video_id {
	brag_and_exit 'No video ID or URL' unless scalar @ARGV;

	$_ = pop @ARGV;

	if (/^\d+$/) {
		$session->{'video_id'} = $_;
	} elsif (/tvrain\.ru.*-(\d+)\/?$/) {
		$session->{'video_id'} = $1;
	} else {
		brag_and_exit "Invalid video ID or URL";
	}

	debug 'video id', $session->{'video_id'} if $options->{'verbose'};
}

sub set_config_file_path {
	$session->{'config_file_path'} = 
		defined $options->{'config'} ? $options->{'config'} :
		"$HOME/$CONFIG_FILE_NAME";

	debug 'config file path', $session->{'config_file_path'} if $options->{'verbose'};
}

sub create_clean_config {
	$config = Config::Tiny->new;
	$config->{_}->{'device_uid'} = &cook_device_uid;

	&write_config;

	print "New device uid created\n";
}

sub read_config {
	if ($options->{'first-run'}) {
		print "Resetting config file per user request\n";
		&create_clean_config;
	} else {
		$config = Config::Tiny->read($session->{'config_file_path'}, 'utf8');

		unless (defined $config && defined $config->{_}->{'device_uid'}) {
			print "Empty / invalid config file → recreating\n";
			&create_clean_config;
		}
	}

	debug 'device uid', $config->{_}->{'device_uid'} if $options->{'verbose'};
}

sub decide_on_temp_dir {
	# TODO: Implement
	if (defined $config->{_}->{'temp_dir'}) {
		if (defined $options->{'temp-dir'}) {
			# Use options, no checking needed
		} else {
			# Check and use config
		}
	} else {
		if (defined $options->{'temp-dir'}) {
			# Use options and write it to config
		} else {
			# It's first run: ask user, check, write to config and use
		}
	}

	check_if_dir_is_wrieable $options->{'temp-dir'}, 'Temporary directory';
}

sub link_if_not_linked {
	return if defined $config->{_}->{'auth'};

	print "Absent / invalid authentication → linking to account\n";
	
	my $startlinking_response = call_api &cook_startlinking_request;

	print "Linking code:\n  $startlinking_response->{'code'}\n\n$linking_hint";

	my $link_request = cook_link_request $startlinking_response->{'oid'};

	print "Trying to link…\n";
	my $link_response;

	do {
		# TODO: Add some kind of sandwatch
		sleep $LINK_POLL_TIMEOUT_SEC;

		$link_response = call_api $link_request;
	} until (defined $link_response->{'user_id'});

	print "Link established\n";

	$config->{_}->{'auth'} = cook_auth $link_response->{'user_id'}, $link_response->{'device_token'};

	&write_config;
}

sub get_me_to_check_auth {
	my $me_response = call_api &cook_me_request;

	brag_and_exit "Authentication failure${relink_hint}" if defined $me_response->{'error'};

	$session->{'me'} = $me_response;

	die "Malformed 'me' response" unless defined $session->{'me'}->{'vip'};

	brag_and_exit "Subscription expired :-(\nPlease visit TVRain.ru and pay them, then come back." if $session->{'me'}->{'vip'} < 0;

	print "Login Ok, subscription active for $session->{'me'}->{'vip'} more days\n";
}

sub get_videos {
	my $videos_response = call_api cook_videos_request $session->{'video_id'};

	brag_and_exit "Video not found\nPlease check Video ID / URL" if defined $videos_response->[0]->{'error'};

	$session->{'videos'} = $videos_response;

	debug 'videos', dumpit $session->{'videos'} if $options->{'verbose'};
}

sub main {
	&read_command_line_options;
	&deal_with_help_requests;
	&interpret_command_line_options;
	&get_video_id;
	&set_config_file_path;
	&read_config;
	&decide_on_temp_dir;
	&link_if_not_linked;
	&get_me_to_check_auth;
	&get_videos;
}


### Main

&main;

